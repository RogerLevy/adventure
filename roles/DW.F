( FOR EDITOR GAME )
WARNING OFF
NEEDS ALLEGRO3
NEEDS DOBS2
NEEDS TILEGRABBER2
NEEDS TILEMAP
NEEDS ANIMATOR

CREATE TPAL 256 CELLS ALLOT
TPAL S" BMP\ADVENTURE.BMP" LOADBMP VALUE ZTBMP
TPAL SETPAL

CREATE ZTSET ZTBMP 16 16 TILESET 
ZTSET VALUE SPRSET

CREATE MAP  40 , 30 ,
40 30 * CELLS ALLOT


TILEMAP MAKE NAMED TESTMAP 
MAP MY .MAP ! ZTSET MY .SET !
40 30 MY .W V! 0 0 MY .CO V! 


( ANIMATION DATA )
( FORMAT: FRAME , PAUSE , FRAME , PAUSE ... -1 , LOOPPTR )
CREATE WALK 14 , 16 , 15 , 16 , #LOOP , WALK ,

DOB MAKE NAMED HITBOX
CELL FIELD .W CELL FIELD .H
CELL FIELD .CLR
CELL FIELD .+X CELL FIELD .+Y
BLUE MY .CLR !
: SHOW-BOX MY POS AT MY .CLR @ COLOR MY .W @ MY .H @ -1 -1 V+ BOX ;
:SHOW SHOW-BOX ;


CREATE TINFO 32 CELLS ALLOT  
: GRP-DAT ( ROW) 2* CELLS TINFO + ;
: GRP-FLAGS ( ROW) GRP-DAT CELL+ ;
: GROUP ( #-V) 4 >> ;
: SOLID? ( FLAGS-F) 0X0001 AND ;

HITBOX MAKE NAMED BOY
ANIMATOR PART .ANIM
CELL FIELD .STEP
0 0 MY .+X V!  16 16 MY .W V!  16 0 MY >POS
SPRSET WALK MY .ANIM INITANIM
: TR MY POS MY .+X V@ V- ;
: SHOW-BMP TR AT MY .ANIM .BMP @ BLIT ;
: TILE-EXEC ( #) DUP GROUP GRP-DAT @ ?EXECUTE ;
: CHECK-TILE MY POS TESTMAP XYMAP-A @ TILE-EXEC ;
:ACT CHECK-TILE MY .ANIM ANIMATE ;
:SHOW SHOW-BMP ;

:NONAME ( #) DROP ; 0 GRP-DAT ! \ NON-SOLID
:NONAME ( #) DROP ." SOLID" ; 1 GRP-DAT ! 0X0001 1 GRP-FLAGS ! \ SOLID

CELL FIELD .DIR
CELL FIELD .NXTDIR
( L R U D )
CREATE DIRXY -1 , 0 , 1 , 0 , -0 , -1 , 0 , 1 , 
: FACE MY .DIR ! ;
: BLK* ( XY-XY) 4 << SWAP 4 << SWAP ;
: XY 2* CELLS DIRXY + V@ ;
: DIR ( -XY) MY .DIR @ XY ;
: CLEAR? XY BLK* MY POS V+ TESTMAP XYMAP-A @ GROUP GRP-FLAGS @ SOLID? NOT ;
: STEP MY .NXTDIR @ MY .DIR ! 16 MY .STEP ! ;
: ?STEP MY .NXTDIR @ -1 <> IF MY .NXTDIR @ CLEAR? IF STEP THEN THEN ;

CREATE FAKEKEYS 4 ALLOT
: DIRKEY FAKEKEYS + C@ ; : PREP MY .NXTDIR ! ;
: NEWDIR? ( -F) 0 4 0 DO I DIRKEY IF I PREP NOT LEAVE THEN LOOP ;
: INPT NEWDIR? NOT IF -1 PREP 4 0 DO I DIRKEY IF I PREP LEAVE THEN LOOP THEN ;
: DMOVE DIR MY .X V+! ;
: STEPPING? MY .STEP @ 0> ;
: MOVE-ME STEPPING? IF -1 MY .STEP +! DMOVE ELSE ?STEP THEN ;
: NOKEYS FAKEKEYS 4 ERASE ;
:ACT INPT MOVE-ME MY .ANIM ANIMATE NOKEYS ;

: BOYLEFT BOY .X @ 0> IF -1 FAKEKEYS 0 + C! THEN ;
: BOYRIGHT BOY .X @ 640 16 - < IF -1 FAKEKEYS 1 + C! THEN ;
: BOYUP BOY .Y @ 0> IF -1 FAKEKEYS 2 + C! THEN ;
: BOYDOWN BOY .Y @ 480 16 - <  IF -1 FAKEKEYS 3 + C! THEN ;

CREATE GOTBL ' BOYLEFT , ' BOYRIGHT , ' BOYUP , ' BOYDOWN ,
: RANDGO 2 RANDOM IF 4 RANDOM CELLS GOTBL + @ EXECUTE THEN ;
: BOYTILE ( N) BOY POS TESTMAP XYMAP-A ! RANDGO ; 

